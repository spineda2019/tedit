const std = @import("std");
const builtin = @import("builtin");

pub fn build(b: *std.Build) std.mem.Allocator.Error!void {
    const target = b.standardTargetOptions(.{});
    const optimize = b.standardOptimizeOption(.{});
    const install_step = b.getInstallStep();

    const compiledb: bool = b.option(
        bool,
        "compiledb",
        "Build compile_commands.json",
    ) orelse false;

    const mod = b.addModule("tedit", .{
        .root_source_file = b.path("src/root.zig"),
        .target = target,
    });
    const modteditexe = b.createModule(.{
        .root_source_file = b.path("src/main.zig"),
        .target = target,
        .optimize = optimize,
        // List of modules available for import in source files part of the
        // root module.
        .imports = &.{
            .{ .name = "tedit", .module = mod },
        },
    });
    const modcpp = b.createModule(.{
        .target = target,
        .optimize = optimize,
        .link_libc = false,
        .link_libcpp = false,
    });
    switch (target.result.os.tag) {
        .linux, .macos, .openbsd, .freebsd => {
            modcpp.addCMacro("TEDIT_POSIX", "");
        },
        .windows => {
            modcpp.addCMacro("TEDIT_WIN32", "");
        },
        else => |other| {
            var init: [27]u8 = comptime .{0} ** 27;
            @memcpy(&init, "Unsupported OS (for now?): ");

            var buf: std.ArrayList(u8) = .initBuffer(&init);
            try buf.appendSlice(b.allocator, @tagName(other));

            @panic(buf.items);
        },
    }

    const cppfiles = comptime .{
        .{ "src/", "cppmain.cpp" },
        .{ "src/", "CStringView.cpp" },
        .{ "src/", "types.cpp" },
    };
    const cppflags = comptime [_][]const u8{
        "-std=c++23",
        "-Wall",
        "-Wextra",
        "-Wpedantic",
        "-Wshadow",
        "-Wconversion",
        "-Werror",
        "-fno-exceptions",
    };

    inline for (cppfiles) |file| {
        const dir_str: []const u8, const file_name: []const u8 = file;
        const full_path_str = dir_str ++ file_name;
        const json_fragment_name = file_name ++ ".json.tmp";

        const full_path = b.path(full_path_str);

        const jsonflags = comptime [_][]const u8{ "-MJ", json_fragment_name };
        const fullflags = cppflags ++ jsonflags;
        modcpp.addCSourceFile(.{
            .file = full_path,
            .flags = &fullflags,
            .language = .cpp,
        });
    }

    const libtedit = b.addLibrary(.{
        .linkage = .static,
        .root_module = mod,
        .name = "tedit",
    });
    const libcpp = b.addLibrary(.{
        .name = "teditcpp",
        .root_module = modcpp,
        .linkage = .static,
    });

    modteditexe.linkLibrary(libcpp);
    modcpp.linkLibrary(libtedit);

    // Here we define an executable. An executable needs to have a root module
    // which needs to expose a `main` function. While we could add a main function
    // to the module defined above, it's sometimes preferable to split business
    // logic and the CLI into two separate modules.
    //
    // If your goal is to create a Zig library for others to use, consider if
    // it might benefit from also exposing a CLI tool. A parser library for a
    // data serialization format could also bundle a CLI syntax checker, for example.
    //
    // If instead your goal is to create an executable, consider if users might
    // be interested in also being able to embed the core functionality of your
    // program in their own executable in order to avoid the overhead involved in
    // subprocessing your CLI tool.
    //
    // If neither case applies to you, feel free to delete the declaration you
    // don't need and to put everything under a single module.
    const exe = b.addExecutable(.{
        .name = "tedit",
        .root_module = modteditexe,
    });

    if (optimize != .Debug) {
        libcpp.lto = .full;
    }

    // This declares intent for the executable to be installed into the
    // install prefix when running `zig build` (i.e. when executing the default
    // step). By default the install prefix is `zig-out/` but can be overridden
    // by passing `--prefix` or `-p`.
    b.installArtifact(exe);

    // This creates a top level step. Top level steps have a name and can be
    // invoked by name when running `zig build` (e.g. `zig build run`).
    // This will evaluate the `run` step rather than the default step.
    // For a top level step to actually do something, it must depend on other
    // steps (e.g. a Run step, as we will see in a moment).
    const run_step = b.step("run", "Run the app");

    // This creates a RunArtifact step in the build graph. A RunArtifact step
    // invokes an executable compiled by Zig. Steps will only be executed by the
    // runner if invoked directly by the user (in the case of top level steps)
    // or if another step depends on it, so it's up to you to define when and
    // how this Run step will be executed. In our case we want to run it when
    // the user runs `zig build run`, so we create a dependency link.
    const run_cmd = b.addRunArtifact(exe);
    run_step.dependOn(&run_cmd.step);

    // By making the run step depend on the default step, it will be run from the
    // installation directory rather than directly from within the cache directory.
    run_cmd.step.dependOn(b.getInstallStep());

    // This allows the user to pass arguments to the application in the build
    // command itself, like this: `zig build run -- arg1 arg2 etc`
    if (b.args) |args| {
        run_cmd.addArgs(args);
    }

    // Creates an executable that will run `test` blocks from the provided module.
    // Here `mod` needs to define a target, which is why earlier we made sure to
    // set the releative field.
    const mod_tests = b.addTest(.{
        .root_module = mod,
    });

    // A run step that will run the test executable.
    const run_mod_tests = b.addRunArtifact(mod_tests);

    // Creates an executable that will run `test` blocks from the executable's
    // root module. Note that test executables only test one module at a time,
    // hence why we have to create two separate ones.
    const exe_tests = b.addTest(.{
        .root_module = exe.root_module,
    });

    // A run step that will run the second test executable.
    const run_exe_tests = b.addRunArtifact(exe_tests);

    // A top level step for running all tests. dependOn can be called multiple
    // times and since the two run steps do not depend on one another, this will
    // make the two of them run in parallel.
    const test_step = b.step("test", "Run tests");
    test_step.dependOn(&run_mod_tests.step);
    test_step.dependOn(&run_exe_tests.step);

    // ******************** Compilation Database Cleanup ******************** //

    const compile_db_step = b.step(
        "compiledb",
        "Build & format compile_commands.json",
    );
    const execompiledb = b.addExecutable(.{
        .name = "compiledb",
        .root_module = b.createModule(.{
            .root_source_file = b.path("cleandb/main.zig"),
            .target = std.Build.resolveTargetQuery(
                b,
                std.Target.Query.fromTarget(&builtin.target),
            ),
            .optimize = optimize,
        }),
    });
    if (compiledb) {
        const runcompiledb = b.addRunArtifact(execompiledb);
        compile_db_step.dependOn(&runcompiledb.step);
        runcompiledb.addFileArg(b.path(""));
        install_step.dependOn(&runcompiledb.step);
    }
}
